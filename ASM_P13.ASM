PROCESSOR P16F887
INCLUDE <P16F887.INC>

__CONFIG _CONFIG1, (_INTRC_OSC_NOCLKOUT & _WDT_OFF & _PWRTE_OFF & _MCLRE_OFF & _CP_OFF & _CPD_OFF & _BOR_OFF & _IESO_OFF & _FCMEN_OFF & _LVP_OFF & _DEBUG_OFF)
__CONFIG _CONFIG2, (_WRT_OFF & _BOR40V)

	;El siguiente programa te permite variar el ciclo de trabajo de los dos PWMs que posee el PIC16F887
	;con una frecuencia de 976Hz.
	;Autor: José Carlos Sánchez 

	T_AL EQU 0x20

	ORG 0x00
	BSF STATUS,RP0      ;BANCO 3
	BSF STATUS,RP1      ;HABILITACIÓN DE PUERTOS ANALOGICOS
	MOVLW 0x60          ;PUERTOS AN5 Y AN6 HABILITADOS
	MOVWF ANSEL
	CLRF ANSELH

	BSF STATUS,RP0       ;BANCO 1
	BCF STATUS,RP1       
	CLRF TRISA          ;CONFIGURACIÓN DE E/S
	CLRF TRISB
	MOVLW 0x06
	MOVWF TRISC         ;PASO1. SALIDAS DE PWM COMO ENTRADAS
	CLRF TRISD
	MOVLW 0x03          ;RE0 Y RE1 COMO ENTRADAS
	MOVWF TRISE

	CLRF ADCON1         ;JUSTIFICACION A LA IZQUIERDA DE AD

	MOVLW 0xFF          ;PASO 2. CONFIGURACIÓN DEL PERIODO DEL PWM
	MOVWF PR2           ;PERIODO DE 1024 us
    
	BANKSEL PORTA
	MOVLW 0x0C          ;PASO 3. CONFIGURACIÓN DEL MÓDULO PWM
	MOVWF CCP1CON       ;Y LOS 2 bmS DEL CICLO DE TRABAJO
	MOVLW 0x0C
	MOVWF CCP2CON

	                    ;PASO 4. CONFIGURACIÓN DEL CICLO DE TRABAJO
	MOVLW 0x80
	MOVWF CCPR1L
	MOVLW 0x80
	MOVWF CCPR2L

                        ;PASO 5. CONFIGURAR E INICIAR EL TMR2.
	BCF PIR1,TMR2IF     ;PASO 5.1 LIMPIAR BANDERA DE DESBORDAMIENTO DEL TMR2
	BSF T2CON,0    	    ;PASO 5.2 PERCALER = 16 (1x)
	BSF T2CON,1
	BSF T2CON, TMR2ON   ;PASO 5.3 ENCENDER MÓDULO TMR2

					    ;PASO 6. HABILITAR SALIDA DEL PWM DESPUES 
E_IP                    ;DE UN UN CICLO DE PWM (DESPUES DE UN DESBORDAMIENTO DEL TMR2)
	BTFSS PIR1,TMR2IF
	GOTO  E_IP
	BANKSEL TRISA
	CLRF  TRISC
	BANKSEL PORTA     

C_P 
	MOVLW 0xD5            ;CONFIGURACIÓN Y HABILITACION
	MOVWF ADCON0          ;DEL CONVERTIDOR AD (AN5)
	BSF   ADCON0,GO_DONE  ;INICIAR CONVERSION
E_C0
	BTFSC ADCON0,GO_DONE  ;ESPERAR RESULTATO
	GOTO  E_C0
	CLRF ADCON0          ;APAGAR CONVERTIDOR AD
	MOVF  ADRESH,W        ;MOVER LOS 8 bMS DEL RESULTADO DE CONVERSION AL
	MOVWF CCPR1L          ;REGISTRO CCPR1L PARA ESTABLECER EL CICLO DE TRABAJO
	CALL  R_AL            ;MOVER LOS 2 bmS DEL RESULTADO A CCP1CON PARA 
	MOVWF CCP1CON         ;ESTABLECER EL CT
	
	MOVLW 0xD9            ;CONFIGURACIÓN Y HABILITACION DEL CONVERTIDOR AD (AN6)
	MOVWF ADCON0          ;INICIAR CONVERSION AD
	BSF ADCON0,GO_DONE    
E_C1 
	BTFSC ADCON0,GO_DONE  ;ESPERAR RESULTADO
	GOTO E_C1
	CLRF ADCON0           ;APAGAR CONVERTIDOR AD
	MOVF ADRESH,W
	MOVWF CCPR2L
	CALL R_AL
	MOVWF CCP2CON

	GOTO C_P

R_AL                    ;SUBRRUTINA QUE DESPLAZA LOS DOS BITS 
	BANKSEL TRISA       ;MENOS SIGNIFICATIVO DEL AD HACIA LOS 
	MOVF ADRESL,W       ;BITS DC1B<1:0> DEL REGISTRO CCP1CON
	BANKSEL PORTA
	MOVWF T_AL
	BCF STATUS,C        
	RRF T_AL,F
	RRF T_AL,F
	MOVLW 0x0C
	IORWF T_AL,W
	RETURN

END